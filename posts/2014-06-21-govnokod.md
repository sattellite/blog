---
tags: perl
noedit: true
---

# Различные хаки с govnokod.ru

Полезные хаки, найденные на [govnokod.ru](http://govnokod.ru)

Выборка из БД, с занесением в хеш по именам столбцов ([pruf](http://govnokod.ru/15973#comment234143)):

```perl
my $query = "SELECT ...";
my $sth = $dbh->prepare($query);
# below technic was described in DBI docs
my %row;
$sth->execute();
$sth->bind_columns( \( @row{ @{$sth->{'NAME_lc'}} }) );
while ($sth->fetch()) {
   if (defined $row{'my_field'}) {
       ...
   }
}
```

Убирание пробельных символов в начале и конце ([pruf](http://govnokod.ru/14421#comment213524)):

```perl
sub trim {
    @_ = @_ ? @_ : $_ if defined wantarray;
    s/^\s+|\s+$//gs for @_ ? @_ : $_;
    wantarray ? @_ : shift
}

trim($foo); # модификация $foo
$bar = trim($foo); # результат в $bar, $foo не меняется
trim(@foo); # модифицируем строки в массиве @foo
@bar = trim(@foo); # @foo не меняется, результат в @bar
```

Найти большее/меньшее число из двух ([pruf](http://govnokod.ru/13520)):

```perl
my ($i, $j) = (42, 13);
my $max = [ $i => $j ]->[ $i <= $j ];
my $min = [ $i => $j ]->[ $i >= $j ];
```

Уникальные ключи из массива хешей ([pruf](http://govnokod.ru/12703)):

```perl
my $data = [
  { one => 1, two => 2 },
  { two => 2, three => 3 },
  { three => 3, four => 4 }
];
my @datakeys = keys %{{ map { %{ $_ } } @{ $data } }};

```
